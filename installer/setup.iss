; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;#pragma include __INCLUDE__ + ";" + ReadReg(HKLM, "Software\Mitrich Software\Inno Download Plugin", "InstallDir")


#define MyAppId "{B5C0DEEA-FFF1-49B8-B923-4E680E4A552D}"
#define MyAppName "Desomnia"
; #define MyAppVersion ""
#define MyAppPublisher "MadWizardDE"
#define MyAppURL "https://madwizard.de/"

#ifdef MyAppVersion
  #if MyAppVersion != ""
    #define MyOutputBaseFilename "Desomnia_" + MyAppVersion
  #else
    #define MyOutputBaseFilename "Desomnia"
  #endif
#else
  #define MyAppVersion ""
  #define MyOutputBaseFilename "Desomnia"
#endif


;#include <idp.iss>

#include "functions.iss"
#include "dependencies/installer.iss"
#include "settings/SystemSessionMonitor.iss"
#include "settings/NetworkMonitor.iss"
#include "settings/DuoStreamMonitor.iss"
#include "settings/NetworkSessionMonitor.iss"
#include "settings/PowerRequestMonitor.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{#MyAppId}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppVerName={#MyAppName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
; Uncomment the following line to run in non administrative install mode (install for current user only).
;PrivilegesRequired=lowest
OutputDir=bin
OutputBaseFilename={#MyOutputBaseFilename}
SolidCompression=yes
WizardStyle=classic
SetupIconFile=..\DesomniaService\Properties\moon.ico
AppModifyPath="{commonappdata}\{#MyAppName}\Installer\setup.exe" /modify=1
UninstallDisplayIcon={app}\DesomniaService.exe
DisableProgramGroupPage=yes
DisableWelcomePage=no

ArchitecturesAllowed=x64 arm64
ArchitecturesInstallIn64BitMode=x64compatible
;ArchitecturesInstallIn64BitMode=x64 arm64

WizardImageFile=resources\Desomnia.bmp

[Types]
Name: "duo"; Description: "DuoStream installation"; Check: IsDuoInstalled
Name: "full"; Description: "Full installation"
Name: "minimal"; Description: "Minimal installation"
Name: "server"; Description: "Server installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom


[Components]
Name: "DesomniaService"; Description: "Desomnia Service"; Types: full minimal duo server custom; Flags: fixed
Name: "DesomniaService\NetworkMonitor"; Description: "Network Monitoring"; Types: full server duo custom; Flags: disablenouninstallwarning;
Name: "plugins"; Description: "Additional Features"; Types: full custom; Flags: disablenouninstallwarning;
Name: "plugins\DuoStreamIntegration"; Description: "DuoStream Integration"; Types: full duo server custom; Flags: disablenouninstallwarning; Check: IsDuoInstalled
Name: "plugins\HyperVSupport"; Description: "Hyper-V Support"; Types: full server custom; Flags: disablenouninstallwarning; Check: IsHyperVInstalled
Name: "plugins\FirewallKnockOperator"; Description: "Firewall Knock Operator"; Types: full custom; Flags: disablenouninstallwarning;
Name: "plugins\DesomniaServiceBridge"; Description: "Interactive Taskbar Icon"; Types: full custom; Flags: disablenouninstallwarning; Check: IsBridgeReady


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; Enables full Unicode-Support in INI-File
Source: "resources\prefs.ini"; DestDir: "{tmp}";

Source: "resources\CheckNetworkInterfaces.ps1"; Flags: dontcopy noencryption
Source: "resources\CheckVirtualSupport.ps1"; Flags: dontcopy noencryption
Source: "resources\network.ico"; Flags: dontcopy noencryption

Source: "build\components\DesomniaServiceConfigurator.exe"; DestDir: "{tmp}"; Flags: ignoreversion

Source: "build\components\service\x64\*"; DestDir: "{app}"; Components: DesomniaService; Flags: ignoreversion recursesubdirs createallsubdirs; Check: IsX64
Source: "build\components\service\arm64\*"; DestDir: "{app}"; Components: DesomniaService; Flags: ignoreversion recursesubdirs createallsubdirs; Check: IsARM64

Source: "build\components\plugins\DuoStreamIntegration\*"; DestDir: "{app}\plugins\DuoStreamIntegration"; Components: plugins\DuoStreamIntegration; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "build\components\plugins\HyperVSupport\*"; DestDir: "{app}\plugins\HyperVSupport"; Components: plugins\HyperVSupport; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "build\components\plugins\FirewallKnockOperator\*"; DestDir: "{app}\plugins\FirewallKnockOperator"; Components: plugins\FirewallKnockOperator; Flags: ignoreversion recursesubdirs createallsubdirs

Source: "build\components\plugins\DesomniaServiceBridge\*"; DestDir: "{app}\plugins\DesomniaServiceBridge"; Components: plugins\DesomniaServiceBridge; Flags: ignoreversion recursesubdirs createallsubdirs skipifsourcedoesntexist

[INI]
Filename: {tmp}\prefs.ini; Section: SystemMonitor; Key: timeout; String: {code:SystemMonitorPrefs|Timeout}; Check: ShouldConfigureSystemMonitor
Filename: {tmp}\prefs.ini; Section: SystemMonitor; Key: idle; String: {code:SystemMonitorPrefs|IdleAction}; Check: ShouldConfigureSystemMonitor
Filename: {tmp}\prefs.ini; Section: SystemMonitor; Key: usage; String: {code:SystemMonitorPrefs|UsageAction}; Check: ShouldConfigureSystemMonitor

Filename: {tmp}\prefs.ini; Section: SessionMonitor; Key: track; String: {code:SessionMonitorPrefs|Track}; Check: ShouldConfigureSessionMonitor
Filename: {tmp}\prefs.ini; Section: SessionMonitor; Key: allowSleepControl; String: {code:SessionMonitorPrefs|AllowSleepControl}; Check: ShouldConfigureSleepControl

Filename: {tmp}\prefs.ini; Section: DuoStreamMonitor; Key: idle; String: {code:DuoStreamMonitorPrefs|IdleAction}; Check: ShouldConfigureDuoStreamMonitor
Filename: {tmp}\prefs.ini; Section: DuoStreamMonitor; Key: demand; String: {code:DuoStreamMonitorPrefs|DemandAction}; Check: ShouldConfigureDuoStreamMonitor

Filename: {tmp}\prefs.ini; Section: NetworkMonitor; Key: interface; String: {code:NetworkMonitorPrefs|Interface}; Check: ShouldConfigureNetworkMonitor
Filename: {tmp}\prefs.ini; Section: NetworkMonitor; Key: name; String: {code:NetworkMonitorPrefs|InterfaceName}; Check: ShouldConfigureNetworkMonitor

Filename: {tmp}\prefs.ini; Section: NetworkSessionMonitor; Key: track; String: {code:NetworkSessionMonitorPrefs|Track}; Check: ShouldConfigureNetworkSessionMonitor
Filename: {tmp}\prefs.ini; Section: PowerRequestMonitor; Key: track; String: {code:PowerRequestMonitorPrefs|Track}; Check: ShouldConfigurePowerRequestMonitor


; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[InstallDelete]
Type: filesandordirs; Name: "{app}\plugins\DuoStreamIntegration"; Check: not WizardIsComponentSelected('plugins\DuoStreamIntegration')
Type: filesandordirs; Name: "{app}\plugins\DesomniaServiceBridge"; Check: not WizardIsComponentSelected('plugins\DesomniaServiceBridge')

[Run]
Filename: "sc.exe"; \
  Parameters: "create DesomniaService binPath= ""{app}\DesomniaService.exe"" start= delayed-auto displayname= Desomnia"; \
  StatusMsg: "Registering Windows service..."; \
  Flags: runhidden waituntilterminated
  
Filename: "sc.exe"; \
  Parameters: "description DesomniaService ""Monitors the resource usage of the system to allow fine-grained control of sleep cycles and trigger events."" "; \
  StatusMsg: "Registering Windows service..."; \
  Flags: runhidden waituntilterminated  

Filename: "sc.exe"; \
  Parameters: "failure DesomniaService reset= 0 actions= restart/60000/restart/60000/restart/60000"; \
  StatusMsg: "Registering Windows service..."; \
  Flags: runhidden waituntilterminated

Filename: "sc.exe"; \
  Parameters: "failureflag DesomniaService 1"; \
  StatusMsg: "Registering Windows service..."; \
  Flags: runhidden waituntilterminated

  
Filename: "{tmp}\DesomniaServiceConfigurator.exe"; \
  Parameters: "init ""{tmp}\prefs.ini"" ""{app}\config\monitor.xml"""; \
  StatusMsg: "Configuring Desomnia..."; \
  Flags: runhidden waituntilterminated; \
  Check: ShouldConfigureDesomnia
  
// Post Installation Checkboxes //

Filename: "{app}\config\monitor.xml"; \
  Verb: "edit"; \
  Description: "Edit configuration"; \
  Flags: postinstall shellexec skipifsilent
  
Filename: "net"; \
  Parameters: "start DesomniaService"; \
  Description: "Start service"; \
  Flags: runhidden waituntilterminated postinstall runascurrentuser


[UninstallRun]
Filename: "net"; \
  Parameters: "stop DesomniaService"; Flags: runhidden waituntilterminated

Filename: "sc.exe"; \
  Parameters: "delete DesomniaService"; \
  Flags: runhidden waituntilterminated

[UninstallDelete]
Type: filesandordirs; Name: "{app}\logs";
Type: filesandordirs; Name: "{commonappdata}\{#MyAppName}\Installer";


[Code]
var
  HasReadConfigFile: Boolean;
  DeleteConfigFiles: Boolean; 
  
function InitializeSetup: Boolean;
begin
  IsReinstall := RegKeyExists(HKEY_LOCAL_MACHINE, UninstallKey);

  HasReadConfigFile := False;
  
  CheckVirtualSupport
  
  Result := True;
end;

procedure VersionLabelOnLinkClick(Sender: TObject; const Link: string; LinkType: TSysLinkType);
var
  ErrorCode: Integer;
begin
  ShellExecAsOriginalUser('open', Link, '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
end;

procedure CreateVersionLabel();
var
  DummyLabel: TLabel;
  VersionLabel: TNewLinkLabel;
begin
  DummyLabel := TLabel.Create(WizardForm);
  DummyLabel.Caption := 'Version';
    
  VersionLabel := TNewLinkLabel.Create(WizardForm);
  //VersionLabel.Caption := 'Version {#MyAppVersion}';
  if not ('{#MyAppVersion}' = '') then
    VersionLabel.Caption := 'Version <a href="https://github.com/MadWizardDE/Desomnia/releases/tag/v{#MyAppVersion}">{#MyAppVersion}</a>';
  //VersionLabel.Font.Color := clGreen;
  //VersionLabel.Font.Style := fsBold;

  VersionLabel.Left := ScaleX(10);
  VersionLabel.Top := WizardForm.CancelButton.Top + (WizardForm.CancelButton.Height / 2) - (DummyLabel.Height / 2)
  VersionLabel.Anchors := [akLeft, akBottom];
  //VersionLabel.Enabled := False;
  VersionLabel.OnLinkClick := @VersionLabelOnLinkClick;

  VersionLabel.UseVisualStyle := False;

  VersionLabel.Parent := WizardForm;
end;


procedure InitializeWizard();
begin
  RemoveAboutMenu
  
  CreateVersionLabel;
  
  SettingsPage := CreateSettingsPage(wpSelectComponents);
  DuoSettingsPage := CreateDuoSettingsPage(SettingsPage.ID);
  NetworkSettingsPage := CreateNetworkSettingsPage(DuoSettingsPage.ID);
  
  RefreshNetworkInterfaces
end;

<event('CurPageChanged')>
procedure AfterPageChanged(CurPageID: Integer);
begin
  if CurPageID = wpSelectComponents then
    if IsReinstall and HasExistingConfig() and not HasReadConfigFile then
      HasReadConfigFile := ReadExistingConfig;
end;

<event('CurStepChanged')>
procedure PostInstallSetup(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
  begin
    AddUninstallerArguments('/SILENT /CONTROLPANEL');
    CopyInstallerTo(ExpandConstant('{commonappdata}\{#MyAppName}\Installer\setup.exe'))
  end;
end;

// --- Uninstaller Logic --- //

function InitializeUninstall(): Boolean;
begin
  DeleteConfigFiles := False;
  Result := True;

  case SuppressibleTaskDialogMsgBox(
      'Desomnia is going to be uninstalled.',
      'Please decide whether to keep your configuration files or not.',   
      mbConfirmation,
      MB_YESNOCANCEL, ['Keep configuration files', 'Delete everything'],
      0, IDYES)
  of
    IDYES:    Result := True;
    IDNO:     DeleteConfigFiles := True;
    IDCANCEL: Result := False;
  end;
end;

<event('CurUninstallStepChanged')>
procedure PostUninstallSetup(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usUninstall then
  begin
    if DeleteConfigFiles = True then
      DelTree(ExpandConstant('{app}\config'), True, True, True);
  end;
end;

